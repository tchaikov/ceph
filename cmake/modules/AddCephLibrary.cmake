function(ceph_add_library name)
  set(for_src TRUE)
  foreach(arg ${ARGN})
    if(for_src)
      if(arg STREQUAL "OBJS")
        set(for_src FALSE)
      else(arg STREQUAL "OBJS")
        list(APPEND srcs ${arg})
      endif(arg STREQUAL "OBJS")
    else(for_src)
      list(APPEND objs ${arg})
    endif(for_src)
  endforeach(arg)
  add_library(${name}-objs OBJECT ${srcs})
  if(BUILD_SHARED_LIBS)
    add_library(${name} SHARED
      $<TARGET_OBJECTS:${name}-objs>)
    if(BUILD_STATIC_LIBS)
      add_library(${name}-static STATIC
        $<TARGET_OBJECTS:${name}-objs> ${objs})
      set_target_properties(${name}-static PROPERTIES
        OUTPUT_NAME ${name})
    endif()
  else(BUILD_SHARED_LIBS)
    add_library(${name} STATIC ${name}-objs ${objs})
  endif(BUILD_SHARED_LIBS)
endfunction(ceph_add_library)

function(ceph_link_libraries name)
  # LINK_PRIVATE instead of PRIVATE is used to backward compatibility with
  # cmake 2.8.11
  if(BUILD_SHARED_LIBS)
    target_link_libraries(${name} LINK_PRIVATE ${ARGN})
  endif()
  if(BUILD_STATIC_LIBS)
    target_link_libraries(${name}-static LINK_PRIVATE ${ARGN})
  endif()
endfunction(ceph_link_libraries)

function(ceph_install_library name)
  if(BUILD_SHARED_LIBS)
    install(TARGETS ${name} DESTINATION lib)
  endif()
  if(BUILD_STATIC_LIBS)
    install(TARGETS ${name}-static DESTINATION lib)
  endif()
endfunction()
